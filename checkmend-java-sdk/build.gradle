group 'uk.co.mattburns'
version '0.3'

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

jar {
    baseName = 'checkmend-java-sdk'
    version =  '0.3'
    manifest {
        attributes 'Implementation-Title': 'checkmend-java-sdk',
                'Implementation-Version': version
    }
}

test {
    systemProperty "CHECKMEND_PARTNER_ID", "${CHECKMEND_PARTNER_ID}"
    systemProperty "CHECKMEND_PERSON_ID", "${CHECKMEND_PERSON_ID}"
    systemProperty "CHECKMEND_SECRET_KEY", "${CHECKMEND_SECRET_KEY}"
}

repositories {
    mavenLocal()
    mavenCentral()
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                    url: "${nexusUrl}/content/repositories/releases") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            snapshotRepository(
                    url: "${nexusUrl}/content/repositories/snapshots") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

        }
    }
}


    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.apache.commons:commons-io:1.3.2'

        compile 'com.google.code.gson:gson:2.2'
        compile 'com.sun.jersey:jersey-client:1.12'
        compile 'com.sun.jersey:jersey-core:1.12'
        compile 'com.sun.jersey.contribs:jersey-multipart:1.12'
        compile 'javax.ws.rs:jsr311-api:1.1.1'
        compile 'org.apache.commons:commons-lang3:3.0.1'
    }